/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'idea'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'net.researchgate.release' version '2.8.1'
    id 'com.github.ben-manes.versions' version '0.29.0'
    id 'com.adarshr.test-logger' version '2.1.0'
}

apply from: "${rootProject.projectDir}/gradle/versions.gradle"
apply from: "${rootProject.projectDir}/gradle/ci.gradle"

group               = 'net.revelc.code'
description         = 'A Gradle plugin to sort the import statements in your Java source files'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext {
    gitRoot           = "https://github.com/revelc"
    gitProject        = "https://github.com/revelc/impsort-gradle-plugin"
    gitURL            = "git@github.com:revelc/impsort-gradle-plugin"

    versions = [
        slf4j  : '1.7.30',
        spock  : "1.3-groovy-2.5"
    ]
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation(gradleApi())
    implementation("com.github.revelc:impsort-maven-plugin:main-SNAPSHOT")

    testImplementation('org.spockframework:spock-core:1.3-groovy-2.5') {
        exclude(module: 'groovy-all')
    }
}

testlogger {
    showStandardStreams false
    slowThreshold 30000
    theme 'mocha'
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier 'groovydoc'
    from groovydoc.destinationDir
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives groovydocJar
    archives javadocJar
}

gradlePlugin {
  plugins {
    impsortPlugin {
      id = 'net.revelc.code.impsort'
      implementationClass = 'net.revelc.code.impsort.ImpsortPlugin'
    }
  }
}

pluginBundle {
    website     = project.gitProject
    vcsUrl      = project.gitProject
    description = project.name
    tags        = [ 'impsort' ]

    plugins {
        impsortPlugin {
            displayName = project.description
        }
    }
}


